/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

namespace org.accordproject.payment.onetime

import org.accordproject.cicero.contract.*
import org.accordproject.cicero.runtime.*
import org.accordproject.money.MonetaryAmount

define transaction InitRequest extends Request {}
define transaction InitResponse extends Response {}

define transaction PaymentReceivedRequest extends Request {}
define transaction PaymentReceivedResponse extends Response {}

define enum ContractLifecycleStatus {
  UNINITIALIZED,
  OBLIGATION_EMITTED,
  COMPLETED
}

define asset OneTimePaymentState extends AccordContractState {
  status : ContractLifecycleStatus
}

define asset OneTimePaymentContract extends AccordContract {
  buyer : AccordParty,
  seller : AccordParty,
  initialAmount : MonetaryAmount
}

contract OneTimePayment over OneTimePaymentContract state OneTimePaymentState {
  clause init() : InitResponse emits PaymentObligation {
    emit PaymentObligation{
      contract: contract,
      promisor: some(contract.buyer),
      promisee: some(contract.seller),
			deadline: none,
      amount: contract.initialAmount,
      description: contract.buyer.partyId ++ " should pay contract amount to " ++ contract.seller.partyId
    };
    set state OneTimePaymentState {
      stateId: "1",
      status: "OBLIGATION_EMITTED"
    };
    return InitResponse{}
  }

  /**
   * Update the contract state once we've recieved a payment
   */
  clause paymentReceived(request : PaymentReceivedRequest): PaymentReceivedResponse {
    enforce state.status = "OBLIGATION_EMITTED";
    set state OneTimePaymentState {
      stateId: state.stateId,
      status: "COMPLETED"
    };
    return PaymentReceivedResponse{}
  }
}

set contract OneTimePayment over OneTimePaymentContract{
  contractId : "1231",
  parties : none,
  buyer : AccordParty{
    partyId: "Dan"
  },
  seller : AccordParty{
    partyId: "Matt"
  },
  initialAmount : MonetaryAmount{
    doubleValue: 1000.0,
    currencyCode: "USD"
  }
}
call init(InitRequest{})
call paymentReceived(PaymentReceivedRequest{})
