/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

namespace org.accordproject.promissorynote

import org.accordproject.cicero.contract.*
import org.accordproject.cicero.runtime.*
import org.accordproject.time.*
import org.accordproject.money.MonetaryAmount

define transaction Payment extends Request {
  amountPaid : MonetaryAmount
}

define transaction Result extends Response {
  outstandingBalance : Double
}

define asset PromissoryNoteContract extends AccordContract {
  amount : MonetaryAmount,
  date : DateTime,
  maker : String,
  interestRate : Double,
  individual : Boolean,
  makerAddress : String,
  lender : String,
  lenderAddress : String,
  principal : MonetaryAmount,
  maturityDate : DateTime,
  defaultDays :  Integer,
  insolvencyDays : Integer,
  jurisdiction : String,
}

define function compoundInterestMultiple(annualInterest : Double, numberOfDays : Double) : Double {
    return (1.0 + annualInterest) ^ (numberOfDays / 365.0)
}

contract PromissoryNote over PromissoryNoteContract {
  clause check(request : Payment) : Result {
    enforce contract.interestRate >= 0.0;
    enforce contract.amount.doubleValue >= 0.0;
    let outstanding = contract.amount.doubleValue - request.amountPaid.doubleValue;
    enforce outstanding >= 0.0;

    let numberOfDays = diffWithUnit(now(),contract.date,~org.accordproject.time.TemporalUnit.days);
    enforce numberOfDays >= 0.0;
    let compounded =  outstanding * compoundInterestMultiple(contract.interestRate, numberOfDays);

    return Result{
      outstandingBalance: compounded
    }
  }
}

set contract PromissoryNote over PromissoryNoteContract{
  contractId : "1231",
  parties : none,
  amount: MonetaryAmount{
    doubleValue: 1000.0,
    currencyCode: "USD"
  },
  date: parse("30 Jan 2018 00:00:00"),
  maker: "Daniel Selman",
  interestRate: 3.8,
  individual: true,
  makerAddress: "1 Main Street",
  lender: "Clause",
  lenderAddress: "246 5th Ave, 3rd Fl, New York, NY 10001",
  principal: MonetaryAmount{
    doubleValue: 500.0,
    currencyCode: "USD"
  },
  maturityDate: parse("20 Jan 2019 00:00:00"),
  defaultDays: 90,
  insolvencyDays: 90,
  jurisdiction: "New York, NY"
}
call init()
call check(Payment{ amountPaid : MonetaryAmount{ doubleValue : 100.0, currencyCode: "USD" } })

