/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.accordproject.cicero.contract.*
import org.accordproject.cicero.runtime.*

define concept LoanModel {rate : Double}
define concept LoanPayment {amount : Double}
define concept LoanState {balance : Double}

define concept LoanPaidOffEvent extends Event {}
define concept LoanDefaultEvent extends Event {}

define function add_monthly_interest (
  balance : Double,
  rate : Double
) : Double
{
    let percent_rate = rate / 100.0;
    let monthly_rate = percent_rate / 12.0;
    let interest = monthly_rate * balance;
    return balance + interest
}

contract StudentLoan over LoanModel state LoanState
{
    clause init(principle : Double) : String {
        set state LoanState {balance: principle};
        return "Welcome to Amazing Bank, LLC."
    }

    clause monthly_payment(req : LoanPayment) : Response {
        set state LoanState {
          balance: add_monthly_interest(state.balance, contract.rate)
        };
        if req.amount <= 0.0 then
          emit LoanDefaultEvent {};
          return Response {}
        else if req.amount >= state.balance then
          set state LoanState {balance: 0.0};
          emit LoanPaidOffEvent {};
          return Response {}
        else
          set state LoanState {balance: state.balance - req.amount};
          return Response {}
    }
}

set contract StudentLoan over LoanModel {rate : 5.1} // ~ the federal rate!
call init(10000.0) // how many semesters does that cover, do you think?

call monthly_payment(LoanPayment {amount: 100.0}) // already feels like a lot
call monthly_payment(LoanPayment {amount: 0.0}) // oh no please send help
call monthly_payment(LoanPayment {amount: -10.0}) // hmm maybe i can cheat the system
call monthly_payment(LoanPayment {amount: 10000000.0}) // hey look i found a job
